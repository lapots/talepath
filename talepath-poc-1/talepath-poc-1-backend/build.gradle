plugins {
    id 'com.sourcemuse.mongo' version "1.0.7"
    id 'idea'
    id 'groovy'
}

sourceSets {
    integrationTest {
        java.srcDir 'src/testInteg/java'
        resources.srcDir 'src/testInteg/resources'
        compileClasspath += sourceSets.main.output + configurations.testCompile
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath + sourceSets.test.runtimeClasspath

    outputs.upToDateWhen { false }
    mustRunAfter test
    runWithMongoDb = true

    useTestNG() {
        suites 'src/testInteg/resources/testng.xml'
    }
    
    /*testLogging {
        showStandardStreams = true
    }*/
    
}

mongo {
    port 10101
    logging 'console'
}

check.dependsOn integrationTest

dependencies {
    annotationProcessor "org.projectlombok:lombok:1.18.6"
    implementation "org.projectlombok:lombok:1.18.6"
    implementation "org.javers:javers-persistence-mongo:5.3.4"
    implementation "org.javers:javers-core:5.3.4"

    testImplementation "org.spockframework:spock-core:1.3-groovy-2.5"
    testImplementation "org.codehaus.groovy:groovy-all:2.5.6"

    integrationTestImplementation "org.testng:testng:6.14.3"
    integrationTestImplementation "org.skyscreamer:jsonassert:1.5.0"
    integrationTestImplementation "com.google.code.gson:gson:2.8.5"
    integrationTestImplementation "commons-io:commons-io:2.6"
}
